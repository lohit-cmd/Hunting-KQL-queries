let TimeWindow = 24h;
let StageMinutes = 60;
// Credential Dump
let CredDumps = DeviceProcessEvents
| where Timestamp >= ago(TimeWindow)
| where FileName in ("ntdsutil.exe","rundll32.exe")
  and ProcessCommandLine has_any("create","full","sekurlsa","lsass")
| project DeviceName, AccountName, CredTime = Timestamp, CmdCred = ProcessCommandLine;
// Proxy Setup
let ProxySetup = DeviceProcessEvents
| where Timestamp >= ago(TimeWindow)
| where FileName == "netsh.exe" and ProcessCommandLine has "portproxy"
| project DeviceName, AccountName, ProxyTime = Timestamp, CmdProxy = ProcessCommandLine;
// Discovery Behavior
let Discovery = DeviceProcessEvents
| where Timestamp >= ago(TimeWindow)
| where FileName in ("powershell.exe","wmic.exe")
  and ProcessCommandLine has_any("Get-WmiObject","vssadmin","netsh","ping")
| summarize DiscoveryCount = count(), DiscoveryTime = min(Timestamp)
    by DeviceName, AccountName
| where DiscoveryCount >= 3;
// Suspicious Logons 
let SuspiciousLogons = DeviceLogonEvents
| where Timestamp >= ago(TimeWindow)
| where ActionType == "LogonSuccess"
  and LogonType in ("Interactive","Remote interactive","Network")
| summarize LogonCount = count(), FirstLogon = min(Timestamp)
    by AccountName, DeviceName
| where LogonCount >= 3;
// Correlate for all Stages
CredDumps
| join kind=leftouter ProxySetup on DeviceName, AccountName
| join kind=leftouter Discovery on DeviceName, AccountName
| join kind=leftouter SuspiciousLogons on DeviceName, AccountName
| extend DeltaCredToProxy = abs(datetime_diff('minute', ProxyTime, CredTime))
| where DeltaCredToProxy <= StageMinutes
| project
    AccountName, DeviceName,
    CredTime, CmdCred,
    ProxyTime, CmdProxy,
    DiscoveryTime, DiscoveryCount,
    FirstLogon, LogonCount,
    DeltaCredToProxy

